main args =                             // Main function
(if (< (len args) 5)                    // Check command-line args
  "Usage: dataparallel.elc host port nitems compms itemsize\n"
(letrec
  host = (item 0 args)                  // Process command-line args,
  port = (ston (item 1 args))           // converting to numbers using
  nitems = (ston (item 2 args))         // ston for numeric args
  compms = (ston (item 3 args))
  itemsize = (ston (item 4 args))

  input = (genstring itemsize)          // Generate input string of
                                        // requested size

  inputlist = (map (!n.input)           // Create input list with n
                   (range 1 nitems))    // copies of input string

  callservice value =                   // Service stub function: connect
    (connect host port                  // to service and send request
    (++ (ntos compms) (++ " " value)))  // containing comp time & data

  results = (map callservice inputlist) // Invoke service for each item
                                        // in input list
in
  (foldr ++ "\nEND\n" results)))        // Print result list, terminated
                                        // by "END" to confirm completion
