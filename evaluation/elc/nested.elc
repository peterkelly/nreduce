printcol n         = (++ (ntos n) " ")

printrow row       = (foldr ++ "\n" (map printcol row))

printmatrix matrix = (foldr ++ "" (map printrow matrix))

creatematrix size  = (map (!x.(range x (+ x (- size 1))))
                          (range 0 (- size 1)))

mkrequest n ms     = (++ (ntos n) (++ " " (ntos ms)))

parseresponse resp = (ston (tail resp))

main args =
(if (< (len args) 4)
    "Usage: nested.elc host port size compms\n"
(letrec host = (item 0 args)
        port = (ston (item 1 args))
        size = (ston (item 2 args))
        compms = (ston (item 3 args))
        callservice n =
          (parseresponse (connect host port (mkrequest n compms)))
        input = (creatematrix size)
        output = (map (!row.map callservice row) input)
 in     (++ (printmatrix output) "END\n")))
