genlist i max =
(if (>= i max)
  nil
  (cons (reverse (numtostring (* 7 i))) (genlist (+ i 1) max)))

flatten lst =
(if lst
  (++ (head lst) (++ "\n" (flatten (tail lst))))
  nil)

mergesort m =
(if (<= (len m) 1)
  m
  (letrec
    middle = (floor (/ (len m) 2))
    left = (prefix middle m)
    right = (skip middle m)
    left2 = (mergesort left)
    right2 = (mergesort right)
  in
    (merge left2 right2)))

merge xlst ylst =
(if xlst
  (if ylst
    (letrec
      x = (head xlst)
      y = (head ylst)
     in
      (if (> (strcmp x y) 0)
        (cons y (merge xlst (tail ylst)))
        (cons x (merge (tail xlst) ylst))))
    xlst)
  ylst)

main args =
(letrec
  n = (if args
        (stringtonum (head args))
        1000)
  input = (genlist 0 n)
  sorted = (mergesort input)
in
  (flatten sorted))
