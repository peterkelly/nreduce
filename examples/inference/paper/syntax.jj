PARSER_BEGIN(Parser)

public class Parser {
}

PARSER_END(Parser)

SKIP : { " " | "\t" | "\n" | "\r" }

TOKEN :
{
  <IF: "if">
| <THEN: "then">
| <ELSE: "else">
| <LETREC: "letrec">
| <IN: "in">
| < IDENTIFIER: <LETTER> (<LETTER>|<DIGIT>)* >
| < OPERATOR: "+"|"-"|"*"|"/"|"%"|"=="|"!="|"<"|"<="|">"|">=" >
| < POPERATOR: "p" ("+"|"-"|"*"|"/"|"%"|"=="|"!="|"<"|"<="|">"|">=") >
| < INTEGER: (<DIGIT>)+ >
| < DOUBLE: (<DIGIT>)+"."(<DIGIT>)+ >
| < #LETTER: ["_","a"-"z","A"-"Z"] >
| < #DIGIT: ["0"-"9"] >
}

Exp Expression() :
{ Exp e; }
{
  ( e = Application() | e = Conditional() | e = Lambda() | e = Letrec() )
  { return e; }
}

Exp Letrec() :
{ 
  Exp tmp = null;
  Exp e = null;
  Decl dlist = null;
  Decl first = null;
  Token name;
  Exp value; }
{

  <LETREC>
  ( name = <IDENTIFIER> "=" value = SimpleExpression()
    {
    Decl newdl = Inference.inst.decl(name.image,value,null);

    if (dlist == null) {
      first = dlist = newdl;
    }
    else {
      dlist.next = newdl;
      dlist = newdl;
    }
    }
  )+ <IN> e = Expression()
  { return Inference.inst.letrec(first,e); }
}

Exp Lambda() :
{ 
  Exp e;
  Token name; }
{
  "!" name = <IDENTIFIER> "." e = Expression()
  { return Inference.inst.lambda(name.image,e); }
}

Exp Application() :
{ 
  Exp tmp = null;
  Exp e = null; }
{
  ( tmp = SimpleExpression()
    { 
      if (e == null)
        e = tmp;
      else
        e = Inference.inst.appl(e,tmp);
    } )+
  { return e; }
}

Exp Conditional() :
{ 
  Exp test = null;
  Exp ifTrue = null;
  Exp ifFalse = null; }
{
  <IF> "(" test = Expression() ")" <THEN>
    ifTrue = SimpleExpression() <ELSE> ifFalse = SimpleExpression()
  { return Inference.inst.cond(test,ifTrue,ifFalse); }
}

Exp SimpleExpression() :
{ Exp e; }
{
  ( e = Int() | e = Double() | e = Ide() | "(" e = Expression() ")" )
  { return e; }
}

Exp Int() :
{ Token t; }
{
  t = <INTEGER>
  { return Inference.inst.iconst(Integer.parseInt(t.image)); }
}

Exp Double() :
{ Token t; }
{
  t = <DOUBLE>
  { return Inference.inst.dconst(Double.parseDouble(t.image)); }
}

Exp Ide() :
{ Token t; }
{
  ( t = <IDENTIFIER> | t = <OPERATOR> | t = <POPERATOR> )
  { return Inference.inst.var(t.image); }
}
