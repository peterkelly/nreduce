printer1 dest partlen lst =
(printer dest (arrayskip partlen lst))

printer dest lst =
(if lst
  (letrec
    partlen = (nchars lst)
   in
    (if partlen
      (seq (printarray dest partlen lst) (printer1 dest partlen lst))
      (seq (print dest (head lst)) (printer dest (tail lst)))))
  (printend dest))

sparklist lst =
(if lst
  (parhead lst (sparklist (tail lst)))
  nil)

parlist lst =
(if lst
  (seq (par (head lst) nil)
    (parlist (tail lst)))
  nil)

spark val =
(seq (sparklist val) val)

__start args =
(if (if (== (len args) 2) (streq (head args) "_connection") nil)
  (letrec
    outputso = (mkconn (forcelist (item 1 args)))
    input = (streamcon outputso)
    realargs = (tail (tail args))
   in
  (seq (printer outputso (main realargs input))
       (exit nil)))

  (letrec
    outputso = (getoutput nil)
    input = nil
   in
  (seq (printer outputso (main args input))
       (exit nil))))

streamfd fd =
(readchunk fd (streamfd fd))

readb filename =
(streamfd (openfd (forcelist filename)))

exists path =
(fexists (forcelist path))

isdir path =
(fisdir (forcelist path))

streamcon so =
(readcon so (streamcon so))

connect host port out =
(letrec
  con = (opencon (forcelist host) port (printer con out))
 in
  (streamcon con))

acceptloop handler so =
(letrec
  connso = (accept so (printer connso (handler instream)))
 instream = (streamcon connso)
 in
  (seq connso
    (cons instream (acceptloop handler so))))

listen port handler =
(acceptloop handler (startlisten port))

readdir filename =
(readdir1 (forcelist filename))

stdin =
(streamfd 0)

readt filename =
(readb filename)

len1 !total lst =
(if lst
  (letrec partlen = (arraysize lst) in
    (len1 (+ total partlen) (arrayskip partlen lst)))
  total)

len lst =
(len1 0 lst)

skip n lst =
(if (<= n 0)
  lst
  (letrec partlen = (arraysize lst) in
    (if (< n partlen)
      (arrayskip n lst)
      (skip (- n partlen) (arrayskip partlen lst)))))

item n lst = (head (skip n lst))

prefix n lst =
(if (<= n 0)
  nil
  (letrec partlen = (arraysize lst) in
    (if (<= n partlen)
      (arrayprefix n lst nil)
      (arrayprefix partlen lst
        (prefix (- n partlen) (arrayskip partlen lst))))))

sub !start !count lst =
(prefix count (skip start lst))

append a b =
(if a
  (letrec partlen = (arraysize a) in
    (arrayprefix partlen a (append (arrayskip partlen a) b)))
  b)

appendn n a b =
(if (== n 2)
  (append a b)
  (appendn (- n 1) (append a b)))

append1 a b =
(append a (cons b nil))

forcelist1 lst =
(if lst
  (letrec
    partlen = (nchars lst)
   in
    (if partlen
      (forcelist1 (arrayskip partlen lst))
      (seq (head lst) (forcelist1 (tail lst)))))
  nil)

forcelist lst =
(seq (forcelist1 lst) lst)

listn n a =
(if (== n 1)
  (cons a nil)
  (consn n (cons a nil)))

consn n a b =
(if (== n 2)
  (append1 a b)
  (consn (- n 1) (append1 a b)))

strcmp a b =
(letrec
  res = (arraystrcmp a b)
 in
  (if res
    res
    (strcmpcons a b)))

strcmpcons a b =
(if a
  (if b
    (if (== (head a) (head b))
      (strcmp (tail a) (tail b))
      (- (head a) (head b)))
    1)
  (if b
    (- 0 1)
    0))


streq a b = (== (strcmp a b) 0)

map f lst =
(if lst
  (cons (f (head lst))
    (map f (tail lst)))
  nil)

apmap f lst =
(if lst
  (append (f (head lst))
    (apmap f (tail lst)))
  nil)

filter f lst =
(if lst
  (if (f (head lst))
    (cons (head lst) (filter f (tail lst)))
    (filter f (tail lst)))
  nil)

reverse1 lst rev =
(if lst
  (reverse1 (tail lst) (cons (head lst) rev))
  rev)

reverse lst =
(reverse1 lst nil)


echo2 dest partlen lst =
(echo1 dest (arrayskip partlen lst))

echo1 dest lst =
(if lst
  (letrec
    partlen = (nchars lst)
   in
    (if partlen
      (seq (printarray dest partlen lst) (echo2 dest partlen lst))
      (seq (print dest (head lst)) (echo1 dest (tail lst)))))
  nil)

echo str = (echo1 (getoutput nil) (forcelist str))

stringtonum str =
(stringtonum1 (forcelist str))

error str =
(error1 (forcelist str))

jnew class args = (_jnew (forcelist class) (forcelist2 args))

forcelist2 lst = (forcelist (map (!x.if (iscons x) (forcelist x) x) lst))

jcall obj method args =
(_jcall (if (iscons obj) (forcelist obj) obj)
        (forcelist method)
        (forcelist2 args))

cxslt source url = (cxslt1 (forcelist source) (forcelist url))

cache key value = (cache1 (forcelist key) (forcelist value))

spawn bytecode to =
(letrec
  cp = (connpair nil)
  a = (head cp)
  b = (tail cp)
  connso = (mkconn (forcelist a))
  from = (streamcon connso)
 in
  (seq (_spawn (forcelist bytecode) b)
  (par (printer connso to)
       from)))

compile code filename = (_compile (forcelist code) (forcelist filename))
