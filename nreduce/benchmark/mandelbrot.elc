mandel Zr Zi Cr Ci res =
(if (== res 4096)
    res
    (letrec
      newZr = (+ (- (* Zr Zr) (* Zi Zi)) Cr)
      newZi = (+ (* 2.0 (* Zr Zi)) Ci)
      mag = (sqrt (+ (* newZr newZr) (* newZi newZi)))
     in
      (if (> mag 2.0)
          res
          (mandel newZr newZi Cr Ci (+ res 1)))))

mandel0 Cr Ci = (mandel 0.0 0.0 Cr Ci 0)

printcell num =
(if (> num 40) "  "
(if (> num 30) "''"
(if (> num 20) "--"
(if (> num 10) "**" "##"))))

mloop minx maxx miny maxy incr x y =
(if (>= x maxx)
    (if (>= (+ y incr) maxy)
        "\n"
        (++ "\n" (mloop minx maxx miny maxy incr minx (+ y incr))))
(++ (printcell (mandel0 x y))
    (mloop minx maxx miny maxy incr (+ x incr) y)))

mandelrange minx maxx miny maxy incr =
(mloop minx maxx miny maxy incr minx miny)


main args =
(letrec
  n = (if args
        (stringtonum (head args))
        32)
  incr = (/ 2 n)
in
  (mandelrange -1.5 0.5 -1.0 1.0 incr))
