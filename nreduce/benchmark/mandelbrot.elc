mandel Zr Zi Cr Ci iterations res =
(if (== res iterations)
    res
    (letrec
      newZr = (+ (- (* Zr Zr) (* Zi Zi)) Cr)
      newZi = (+ (* 2.0 (* Zr Zi)) Ci)
      mag = (sqrt (+ (* newZr newZr) (* newZi newZi)))
     in
      (if (> mag 2.0)
          res
          (mandel newZr newZi Cr Ci iterations (+ res 1)))))

mandel0 Cr Ci iterations = (mandel 0.0 0.0 Cr Ci iterations 0)

printcell num =
(if (> num 40) "  "
(if (> num 30) "''"
(if (> num 20) "--"
(if (> num 10) "**" "##"))))

mloop minx maxx miny maxy incr x y =
(++ (printcell (mandel0 x y 4096))
(if (> x maxx)
    (if (> (+ y incr) maxy)
        (++ "\n" nil)
        (++ "\n" 
          (mloop minx maxx miny maxy incr minx (+ y incr))))
    (mloop minx maxx miny maxy incr (+ x incr) y)))

mandelrange minx maxx miny maxy incr =
(mloop minx maxx miny maxy incr minx miny)


main args =
(letrec
  incr = (if args (stringtonum (head args)) 0.01)
in
  (++ "incr = " (++ (numtostring incr) (++ "\n"
  (mandelrange -1.5 0.5 -1.0 1.0 incr)))))
