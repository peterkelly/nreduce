// No letrecs or top-level definitions (except for main)
// loop defined using continuation as well

main =
((!Y1.!Y1cont.!Y2cont.
  (Y2cont

    // almost-even
    (!even.!odd.!n.
      (if (== n 0)
          1
          (odd (- n 1))))

    // almost-odd
    (!even.!odd.!n.
     (if (== n 0)
         nil
         (even (- n 1))))

    (!even.!odd.

       // loop
       (Y1cont
         (!self.!n.!max.
           (if (> n max)
               nil
               (++ (numtostring n)
                   (++ ": "
                       (++ (if (even n) "even\n" "odd\n")
                           (self (+ n 1) max))))))
         (!loop.

           //main
           (loop 0 10))))))

  // Y1
  (!f.((!x.f (x x)) (!x.f (x x))))

  // Y1cont
  (!f.!cont.cont ((!x.f (x x)) (!x.f (x x))))

  // Y2cont
  ((!P.
    ((!Y2.
        (!a.!b.!cont.(cont (Y2 a a b) (Y2 b a b))))
      // Y2
      (!f.!a.!b.((P f) (P a) (P b)))))
    // P
    (!f.!x.!y.f (x x y) (y x y))))
