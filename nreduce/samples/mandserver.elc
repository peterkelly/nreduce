import http

mandel Zr Zi Cr Ci iterations res =
  (if (= res iterations)
      res
      (letrec newZr = (+ (- (* Zr Zr) (* Zi Zi)) Cr)
              newZi = (+ (* 2.0 (* Zr Zi)) Ci)
              mag = (sqrt (+ (* newZr newZr) (* newZi newZi)))
       in
           (if (> mag 2.0)
               res
               (mandel newZr newZi Cr Ci iterations (+ res 1)))))

mandel0 Cr Ci iterations = (mandel 0.0 0.0 Cr Ci iterations 0)

printcell num =
(append "<td class=\"col"
  (append (numtostring num)
      "\"></td>"))

mloop minx maxx xincr miny maxy yincr x y =
(if (> x maxx)
    (if (> y maxy)
        (append "</tr></table></body></html>" nil)
        (append "</tr></tr>" (mloop minx maxx xincr miny maxy yincr minx (+ y yincr))))
    (append (printcell (mandel0 x y 255))
          (mloop minx maxx xincr miny maxy yincr (+ x xincr) y)))

mandelrange minx maxx xincr miny maxy yincr =
(mloop minx (+ maxx xincr) xincr miny maxy yincr minx miny)

domandel = (mandelrange -1.5 0.5 0.015 -1.0 1.0 0.015)

plainheader =
(appendn 4
    "HTTP/1.1 200 OK\r\n"
    "Content-Type: text/plain; charset=UTF-8\r\n"
    "Connection: close\r\n"
    "\r\n")

httpheader =
(appendn 4
    "HTTP/1.1 200 OK\r\n"
    "Content-Type: text/html; charset=UTF-8\r\n"
    "Connection: close\r\n"
    "\r\n")

process method uri version headers body =
(if (streq uri "/mandserver.css")
  (append plainheader (readb "samples/mandserver.css"))
  (append httpheader
    (append "<html><head>"
      (append "<link rel=\"stylesheet\" type=\"text/css\" href=\"/mandserver.css\">"
        (append "</head><body><table border=\"0\" cellpadding=\"0\" cellspacing=\"0\"><tr>"
          domandel)))))

handler stream =
(if stream (http:parse_request stream process) nil)

main =
(parlist (listen "0.0.0.0" 1234 handler))
